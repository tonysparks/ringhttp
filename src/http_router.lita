import "std/array"
import "std/string"
import "std/assert"
import "std/libc"
import "std/mem"
import "std/log"

import "http_request"
import "http_response"
import "http_common"
import "http_parser"

public struct RequestHandlerContext {
    allocator: *const Allocator
    request: *HttpRequest
    response: *HttpResponse
    userData: *void

    match: *RouteMatch
}

public struct HttpController {
    userData: *void
    callback: func(*RequestHandlerContext) : i32;
}

public struct RouteMatch {
    template: *HttpPathTemplate
    values: HttpPathValues
    controller: HttpController
}


internal const MAX_METHODS = 10

internal struct Router {
    methods: [MAX_METHODS]Array<Route>
}

struct Route {
    template: HttpPathTemplate
    controller: HttpController
}

internal func (this: *Router) init(allocator: *const Allocator) {
    for(var i = 0; i < MAX_METHODS; i+=1) {
        this.methods[i].init(128, allocator)
    }
}

internal func (this: *Router) free() {
    for(var i = 0; i < MAX_METHODS; i+=1) {
        this.methods[i].free()
    }
}

internal func (this: *Router) register(
    method: HttpMethod,
    route: *const char,
    controller: HttpController) : bool {

    var template: HttpPathTemplate;
    if(ParsePathTemplateStr(route, &template) != ParseStatus.OK) {
        return false
    }


    this.methods[method].add(Route {
        .template = template,
        .controller = controller
    })

    return true
}

internal func (this: *Router) match(
    method: HttpMethod,
    path: *String,
    result: *RouteMatch
) : bool {
    var routes = &this.methods[method]

    // TODO: Do something much smarter here...
    //  Trie? Radix?

    for(var i = 0; i < routes.size(); i+=1) {
        var route = routes.getPtr(i)

        if(CheckMatch(&route.template, path)) {
            result.template = &route.template
            result.controller = route.controller

            return ParsePathValues(&route.template, path, &result.values) == ParseStatus.OK
        }
    }

    return false
}

func CheckMatch(route: *HttpPathTemplate, path: *String) : bool {
    var numberOfEntries = 0
    var index = 0
    var i = 0

    while(index < route.template.length) {
        if(i >= path.length) {
            return false
        }

        var a = route.template.buffer[index]
        if(a == '{') {

            // eat the value
            while(i < path.length) {
                var b = path.buffer[i]
                if(b == '/') {
                    break
                }

                i+=1
            }

            // skip ahead to next path element
            while(index < route.template.length) {
                a = route.template.buffer[index]
                if(a == '/') {
                    break
                }

                index+=1
            }

            numberOfEntries += 1
        }
        else {
            var b = path.buffer[i]
            if(a != b) {
                return false
            }
        }

        index += 1
        i += 1
    }

    // if there is still more path elements, this shouldn't be
    // a match...
    if(i < path.length) {
        return false
    }

    return numberOfEntries == route.numberOfEntries
}


func assertCheckMatch(t: *const char, p: *const char, expected: bool) {
    var template: HttpPathTemplate;
    ParsePathTemplateStr(t, &template)

    var path = StringInit(p)
    assert(CheckMatch(&template, path) == expected)
}

@test
func testCheckMatch() {
    assertCheckMatch("/{id}/{a}", "/123/b", true)
    assertCheckMatch("/{id}/{a}", "/123/", false)
    assertCheckMatch("/{id}/", "/123/b", false)
    assertCheckMatch("{id}/", "123/", true)
    assertCheckMatch("{id}", "123", true)
    assertCheckMatch("/{id}", "/123/b", false)
    assertCheckMatch("/{id}", "/123", true)
    assertCheckMatch("", "", true)
    assertCheckMatch("", "/abc", false)
}